(load "./jump.carp")

(defn city-speed [speed]
  (match-ref speed
    Speed.One   1
    Speed.Two   1
    Speed.Three 2
    Speed.Four  2))

(defn ground-speed [speed]
  (match-ref speed
    Speed.One   2
    Speed.Two   3
    Speed.Three 4
    Speed.Four  5))

(defn jump-button? []
 (with Key
  (or (is-down? A) (is-down? Up))))

(defn update-player-position [player]
  (with Key
    (with Player
      (match-ref (state &player)
        PlayerState.Jumping (update-y player &(fn [y] (JumpMap.jumping y)))
        PlayerState.Falling (update-y player &(fn [y] (JumpMap.falling y)))
        _                   player))))

(defn update-player-state [player]
  (with Key
    (with Player
      (match-ref (state &player)
        (PlayerState.Running _) (if (jump-button?)
                                  (set-state player (PlayerState.Jumping))
                                  (if (is-down? Down)
                                    (set-state player (PlayerState.Dodging))
                                    (tick player)))
        PlayerState.Jumping     (if (or (<= (y &player) &45)
                                        (and (<= (y &player) &53) (not (jump-button?))))
                                  (set-state player (PlayerState.Falling))
                                  player)
        PlayerState.Falling     (if (>= (y &player) &88)
                                  (set-state player (PlayerState.Running 1))
                                  player)
        PlayerState.Dodging     (if (is-down? Down)
                                  (set-state player (PlayerState.Dodging))
                                  (set-state player (PlayerState.Running 0)))))))

(defn furthest-obstacle [obs]
  (Array.reduce
    &(fn [acc ob]
      (if (>= (Obstacle.x ob) &acc)
          @(Obstacle.x ob)
          acc))
    0
    obs))

(defn min-distance-obstacles [speed]
  (match-ref speed
    Speed.Four 140
    _          110))

(defn too-close-to-other-obstacle? [new-pos furthest-x speed]
 (let [distance &(min-distance-obstacles speed)]
  (or (and (<= new-pos furthest-x) (>= @new-pos (- @furthest-x @distance)))
      (and (>= new-pos furthest-x) (<= @new-pos (+ @furthest-x @distance))))))

(defn random-reset-position [furthest-x speed]
  (let [new-pos (+ (Rand.range 0 120) Video.screen-width)]
    (if (too-close-to-other-obstacle? &new-pos furthest-x speed)
      (+ @furthest-x (Rand.range (+ (min-distance-obstacles speed) 20) 400))
      new-pos)))

(defn update-obstacle-position [obs furthest-x speed]
  (Obstacle.init
    (if (<= (Obstacle.x &obs) &-16)
     (random-reset-position furthest-x speed)
     (- @(Obstacle.x &obs) (ground-speed speed)))
    @(Obstacle.passed &obs)
    (if (<= (Obstacle.x &obs) &-16)
     (if (= 0 (Rand.range 0 5)) (ObstacleType.High) (ObstacleType.Low))
     @(Obstacle.ty &obs))))

(defn update-obstacles-position [obstacles speed]
  (let [furthest-x (furthest-obstacle &obstacles)]
    (Array.endo-map
     &(fn [obs] (update-obstacle-position obs &furthest-x speed))
     obstacles)))

(defn update-position [world]
  (let [speed (World.speed &world)
        obs-updater &(fn [obstacles] (update-obstacles-position obstacles speed))]
    (match-ref (World.state &world)
      WorldState.Playing
      (=> world
          (World.update-p &update-player-position)
          (World.update-obstacles obs-updater))
      _ world)))

(defn update-world-state [world]
  (=> world
    (World.tick)
    ((fn [wrld]
      (if (Key.hit? Key.Start)
          (match-ref (World.state &wrld)
             WorldState.Playing       (World.set-state wrld (WorldState.Paused))
             (WorldState.Start tik)   (do (Rand.set-seed @tik) (World.start wrld))
             WorldState.Paused        (World.set-state wrld (WorldState.Playing))
             _                        wrld)

          (match-ref (World.state &wrld)
             (WorldState.Intro tik)   (if (>= tik &0) (World.set-state wrld (WorldState.Playing)) wrld)
             (WorldState.Outro tik)   (if (<= tik &-110) (World.set-state wrld (WorldState.new-start)) wrld)
             _                        wrld))))))

(defn check-for-collisions [world]
  (let [world* &world]
   (World.update-state world
    &(fn [state]
      (if (Array.any?
           &(fn [obs] (rect-colliding? (World.p world*)
                                       obs))
           (World.obstacles world*))
        (WorldState.new-outro)
        (WorldState.Playing))))))

(defn obs-past-player [obs] (< (Obstacle.x obs) &8))

(defn mark-obstacles-as-passed [obstacles]
  (Array.endo-map
    &(fn [obs]
      (let [past (obs-past-player &obs)]
        (Obstacle.set-passed obs past)))
    obstacles))

(defn score-from-obstacle [obs]
 (if @(Obstacle.passed obs)
  0
  (if (obs-past-player obs) 1 0)))

(defn calculate-score [world]
  (let [new-score
        (Array.reduce &(fn [acc obs] (+ acc (score-from-obstacle obs)))
                      @(World.score &world)
                      (World.obstacles &world))]
    (=> world
      (World.set-score new-score)
      (World.update-obstacles &(fn [obstacles] (mark-obstacles-as-passed obstacles))))))

(defn update-backgrounds [world]
  (match-ref (World.state &world)
     (WorldState.Paused) world
     _ (let [back-zero (+ @(World.back-zero-x &world) (city-speed (World.speed &world)))
             back-one  (+ @(World.back-one-x &world) (ground-speed (World.speed &world)))]
         (=> world
          (World.set-back-zero-x back-zero)
          (World.set-back-one-x  back-one)))))

(defn update-state-if-playing [world]
  (match-ref (World.state &world)
     WorldState.Playing
     (=> world
         (calculate-score)
         (World.update-p &update-player-state)
         (check-for-collisions))
     _ world))

(defn update-speed [world]
  (let [score (World.score &world)]
   (World.set-speed world
                    (if (< score &10)
                     (Speed.One)
                     (if (< score &30)
                       (Speed.Two)
                       (if (< score &50)
                        (Speed.Three)
                        (Speed.Four)))))))

(defn update-state [world]
  (=> world
      (update-world-state)
      (update-speed)
      (update-backgrounds)
      (update-state-if-playing)))

(sig update (Fn [World] World))
(defn update [world]
  (=> world
    (update-position)
    (update-state)))

